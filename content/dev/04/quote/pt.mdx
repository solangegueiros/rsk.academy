---
title: 'Quote dApp'
description: 'Quote dApp'
---

Vamos criar um smart contract para salvar uma mensagem ou citação no Blockchain.

O smart contract chama-se **Quote**.

Utilizaremos Remix para criar o smart contract.

Abra [remix.ethereum.org](http://remix.ethereum.org/)

## Crie um smart contract

1. Clique no 2o botão do lado esquerdo - file explorer
2. Clique no primeiro símbolo à direita do browser, com a mensagem **create a new file**

![create a new file](/img/courses/dev/04/quote/image-01.png)

Nome do arquivo / file name: **Quote.sol**

Copie e cole esse exemplo:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.1;

contract Quote {
  address public owner;
  string private message;
  mapping (address => bool) public whiteList;

  constructor() {
      owner = msg.sender;
      whiteList[msg.sender] = true;
  }

  modifier onlyOwner {
      require(msg.sender == owner,"Only owner");
      _;
  }

  modifier onlyWhitelist {
      require(whiteList[msg.sender] == true, "Only whitelist");
      _;
  }

  function setQuote(string memory _message) public onlyWhitelist {
      message = _message;
  }

  function getQuote() public view returns (string memory) {
      return message;
  }

  function addMember (address _member) public onlyOwner {
      whiteList[_member] = true;
  }

  function delMember (address _member) public onlyOwner {
      whiteList[_member] = false;
  }
}

```

## Quote.sol

Vamos entender o smart contract.

### Variáveis

**owner**

```solidity
address public owner;
```

Endereço do criador do smart contract;

**message**

```solidity
string private message;
```

Mensagem guardada no Blockchain.

**whiteList**

```solidity
mapping (address => bool) public whiteList;
```

Lista de endereços definindo quem pode atualizar a variável **message**.

### Constructor

Função que é executada SOMENTE durante a publicação do smart contract.

```solidity
owner = msg.sender;
whiteList[msg.sender] = true;
```

Estamos inicializando váriáveis.

**msg.sender** é o endereço que enviou a transação de criação do smart contract.

- Armazenamos o **msg.sender** na variável **owner**.
- Adicionamos na **whiteList**, ou seja, dizemos que ele está autorizado, com o valor booleano **true**.

### Modifier

O modificador define requisitos que serão verificados automaticamente antes de executar uma função.

É muito comum usar o comando **require** dentro de um modificador.

#### onlyOwner

```solidity
modifier onlyOwner {
    require(msg.sender == owner,"Only owner");
    _;
}
```

Este modificador verifica se quem está enviando a transação é o **owner** do smart contract.
Se não for, é enviada a mensagem de erro **Only owner**.

#### onlyWhitelist

```solidity
modifier onlyWhitelist {
    require(whiteList[msg.sender] == true, "Only whitelist");
    _;
}
```

Este modificador verifica se quem está enviando a transação tem o valor **true** na **whiteList**.
Se não for, é enviada a mensagem de erro **Only whitelist**.

> Caso o endereço não esteja previamente inicializado na whitelist, o valor **default** é **false**.

### Message functions

São as funções relacionadas à mensagem armazenada no smart contract.

#### setQuote

```solidity
function setQuote(string memory _message) public onlyWhitelist {
    message = _message;
}

```

Função para gravar uma mensagem, ou seja, atualizar a variável **message**.

Utiliza o modificador **onlyWhitelist**, ou seja,
apenas quem está na whitelist pode alterar a mensagem armazenada.

#### getQuote

```solidity
function getQuote() public view returns (string memory) {
    return message;
}

```

Função para retornar a mensagem salva na variável **message**.

### Member administration

Temos funções para administrar a whitelist, ou seja, definir quem está ou não na whitelist.

Estas funções são controladas pelo modificador **onlyOwner**, ou seja,
apenas o criador do smart contract pode administrar a whitelist.

#### addMember

```solidity
function addMember (address _member) public onlyOwner {
    whiteList[_member] = true;
}

```

Define que para um determinado endereço, que é a chave, será armazenado o valor **true**.

#### delMember

```solidity
function delMember (address _member) public onlyOwner {
    whiteList[_member] = false;
}

```

Define que para um determinado endereço, que é a chave, será armazenado o valor **false**, que é o valor padrão.
Se el algum momento o endereço teve o valor **true** associado a ele, é preciso definir o valor **false** para que ele não tenha mais acesso.

## Compile o smart contract

Se você habilitou o auto-compile,
o smart contract já estará compilado e
aparecerá um sinal verde no 3o botão com a mensagem `compilation successful`.

![compilation successful](/img/courses/dev/04/quote/image-02.png)

Se você não habilitou o auto-compile, siga estes passos:

1. Clique no 3o botão do lado esquerdo - `Solidity compiler`
2. Clique no botão `Compile Quote.sol`
3. Verifique o sinal verde no 3o botão com a mensagem `compilation successful`

## Deploy a smart contract on RSK testnet

### Pré requisitos

1. Selecione a RSK network em sua web wallet, Nifty ou Metamask.
2. Tenha tR-BTC na sua carteira.
3. No Remix, em **Deploy and run transactions**,
   em **Environment**, garanta que selecionou a opção **Injected Web3**.

## Faça o deploy

No Remix, vá no 4o ícone: **Deploy and run transactions**.

Como nós só temos um smart contract, ele é automaticamente selecionado na lista.

Clique no botão **Deploy**.

Sua web wallet abrirá uma janela do tipo "popup" para confirmar a transação configurada pelo Remix para fazer a publicação do smart contract.

Clique em **confirm / submit**.

Na parte abaixo, à direita, aparecerá a mensagem: _creation of Name pending..._

Lembre-se que um bloco na rede RSK leva, em média, 30 segundos para ser minerado,
então devemos esperar este tempo.

## Dirección de contrato inteligente

Copia la dirección de tu contrato inteligente y guárdelo.
Se utilizará en otros momentos del curso.

Hay algunas formas de copiarlo:

1. En Remix

Después de que se publique el contrato inteligente con Remix,
podemos ver su instancia en el panel izquierdo,
en la parte inferior de **deploy and run transactions**.

Vayas a la sección **deployed contracts**,
localice el contrato inteligente y haz clic en el icono de copia.

Este é o endereço do meu smart contract **quote**:

```
0xF2FD988A92484ff16Edd5BD9DAbE5eb809c5B3Dd
```

## Interagindo com o smart contract

Da mesma foram que já interagimos com o smart contract publicado na JavascriptVM,
também podemos usar o Remix para interagir com um smart contract publicado em qualquer rede.

A única alteração é o **Environment**, ou seja, a rede conectada, que agora é **Injected Web3**.

Em **deployed contracts**, no smart contract, clique em **>** para enxergar os detalhes.

![deployed contracts](/img/courses/dev/04/quote/image-03.png)

## Considerações finais

Parabéns!

Você publicou o smart contract **Quote** no Blockchain RSK Testnet e interagiu com ele.
